@ECHO OFF
SETLOCAL
  CALL :NORMALIZEPATH "C:\dev-tools\bin\msvc-no-vcvars"
  PUSHD %RETVAL%
    CALL "setup_cl_generic.bat" x64
  POPD

  SET NO_OUTPUT=^>nul 2^>nul
  SET PROJECT_ALIAS=rake

  SET PREPROCESSOR=/D_CRT_SECURE_NO_WARNINGS
  SET EXCEPTIONS=/GR- /EHa-
  SET WARNINGS=/WX /W4

  REM Both C11 and C17 standard support requires VS2019 v16.8 or higher.
  SET STANDARD=/std:c11
  SET SUBSYSTEM=/SUBSYSTEM:CONSOLE

  SET COMMON_COMPILER_FLAGS=/nologo %PREPROCESSOR% %STANDARD% ^
                            %EXCEPTIONS% %SECURITY% %WARNINGS% /FC
  SET COMMON_LINKER_FLAGS=/INCREMENTAL:NO /OPT:REF %SUBSYSTEM%
  SET OPTIMIZATION_MODE=/O2

  CL ^
    %COMMON_COMPILER_FLAGS% %OPTIMIZATION_MODE% ^
    /Z7 "%PROJECT_ALIAS%.c" ^
    %INTERNAL_LIBS% /link %COMMON_LINKER_FLAGS%

ENDLOCAL

:: ========== FUNCTIONS ==========
EXIT /B

:NORMALIZEPATH
  SET RETVAL=%~f1
  GOTO :EOF
